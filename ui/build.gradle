apply plugin: 'kotlin-multiplatform'

kotlin {
    targets {
        if (project.property("lanark.sdl2.enable") != "false") {
            fromPreset(presets.macosX64, 'sdl2macos')
            fromPreset(presets.iosX64, 'sdl2ios')
        }
        
        fromPreset(presets.jvm, 'lwjgl')
        configure([lwjgl]) {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }

    sourceSets {
        commonMain {
            kotlin.srcDirs = ["common"]
            dependencies {
                implementation project(":core")
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
            }
        }
        commonTest {
            kotlin.srcDirs = ["test"]
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }

        if (project.property("lanark.sdl2.enable") != "false") {
            sdl2macosMain {
                kotlin.srcDirs = ["sdl2"]
            }
            sdl2iosMain {
                dependsOn sdl2macosMain
            }
        }

        lwjglMain {
            kotlin.srcDirs = ["lwjgl"]
        }
    }
}

kotlin.targets.all {
    compilations.all { compilation ->
        tasks.getByName(compilation.compileKotlinTaskName) {
            kotlinOptions.freeCompilerArgs = ["-Xuse-experimental=kotlin.ExperimentalUnsignedTypes"]
        }
    }
}
